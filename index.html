<!DOCTYPE html>
<html lang="EN">

<head>
    <title>GC21 Data Visualization With WebGL</title>
    <meta charset="UTF-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <link href="./asset/css/style.css" type="text/css" rel="stylesheet" />
    <link href="./favicon.ico" rel="icon" type="image/x-icon" />
    <!-- fonts -->
    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Readex+Pro:wght@200&display=swap" rel="stylesheet">
</head>

<body>
    <canvas id="gl_canvas"></canvas>
    <div id="box">
        <div id="boxinner"></div>
    </div>
    <button id="btn-refresh" onclick="location.reload()">Refresh</button>
    <script type="x-shader/x-vertex" id="earth.vert">#version 300 es
layout(location = 0) in vec3 vPosition;
layout(location = 1) in vec2 vinTexturecoord;
layout(location = 2) in vec2 vinCloudTexturecoord;
layout(location = 3) in vec3 vinTextureNormal;
uniform mat4 uProjectionMatrix;
uniform mat4 uViewMatrix;
uniform mat4 uWorldMatrix;
out vec2 voutTexturecoord;
out vec2 voutCloudTexturecoord;
out vec3 voutTextureNormal;

void main(void) {
    gl_Position = uProjectionMatrix * uViewMatrix * uWorldMatrix * vec4(vPosition, 1.0);
    voutTexturecoord = vinTexturecoord;
    voutCloudTexturecoord = vinCloudTexturecoord;
    voutTextureNormal = vinTextureNormal;
 }
        </script>
    <script type="x-shader/x-fragment" id="earth.frag">#version 300 es
#ifdef GL_FRAGMENT_PRECISION_HIGH
    precision highp float;
#else
    precision mediump float;
#endif
in vec2 voutTexturecoord;
in vec2 voutCloudTexturecoord;
in vec3 voutTextureNormal;
uniform sampler2D uDayTexture;
uniform sampler2D uNightTexture;
uniform sampler2D uCloudTexture;
uniform vec3 uLightDir;
out vec4 Color;

void main(void) {
    const float dffborder = 0.25;
    vec3 day = texture(uDayTexture, voutTexturecoord).rgb;
    vec3 night = texture(uNightTexture, voutTexturecoord).rgb;
    vec4 cloud = texture(uCloudTexture, voutCloudTexturecoord).rgba;
    float shade = max(0.0, dot(voutTextureNormal, uLightDir) + dffborder);

    Color = vec4(day - shade, 1.0);

    if (shade >= dffborder) {
        if (night.r > night.b) {
            Color = vec4(night, 1.0);
        }
    }

    if (cloud.a >= 1.0) {
        Color = cloud;
    }
}
        </script>
    <script type="x-shader/x-vertex" id="flight.vert">#version 300 es
layout(location = 4) in vec3 vPosition;
uniform mat4 uProjectionMatrix;
uniform mat4 uViewMatrix;
uniform mat4 uWorldMatrix;
void main(void) {
    gl_Position = uProjectionMatrix * uViewMatrix * uWorldMatrix * vec4(vPosition, 1.0);
    gl_PointSize = 3.0;
}
        </script>
    <script type="x-shader/x-fragment" id="flight.frag">#version 300 es
#ifdef GL_FRAGMENT_PRECISION_HIGH
    precision highp float;
#else
    precision mediump float;
#endif
out vec4 Color;
void main(void) {
    Color = vec4(1.0, 0.0, 0.0, 1.0);
}
        </script>
    <script type="x-shader/x-vertex" id="pick.vert">#version 300 es
layout(location = 5) in vec3 vPosition;
layout(location = 6) in vec4 vinColor; //picker infomation
uniform mat4 uProjectionMatrix;
uniform mat4 uViewMatrix;
uniform mat4 uWorldMatrix;
out vec4 voutColor;
void main(void) {
    gl_Position = uProjectionMatrix * uViewMatrix * uWorldMatrix * vec4(vPosition, 1.0);
    gl_PointSize = 3.0;
    voutColor = vinColor;
}
        </script>
    <script type="x-shader/x-fragment" id="pick.frag">#version 300 es
#ifdef GL_FRAGMENT_PRECISION_HIGH
    precision highp float;
#else
    precision mediump float;
#endif
in vec4 voutColor; //picker information
out vec4 Color;

void main(void) {
    Color = voutColor;
}
        </script>
    <script type="x-shader/x-vertex" id="atmo.vert">#version 300 es
layout(location = 7) in vec3 position;
uniform mat4 uProjectionMatrix;
uniform mat4 uViewMatrix;
uniform mat4 uWorldMatrix;
uniform vec3 v3CameraPos;
uniform vec3 v3LightPosition;	// The direction vector to the light source
uniform vec3 v3InvWavelength;	// 1 / pow(wavelength, 4) for the red, green, and blue channels
uniform float fCameraHeight;	// The camera's current height
uniform float fCameraHeight2;	// fCameraHeight^2
uniform float fOuterRadius;		// The outer (atmosphere) radius
uniform float fOuterRadius2;	// fOuterRadius^2
uniform float fInnerRadius;		// The inner (planetary) radius
uniform float fInnerRadius2;	// fInnerRadius^2
uniform float fKrESun;			// Kr * ESun
uniform float fKmESun;			// Km * ESun
uniform float fKr4PI;			// Kr * 4 * PI
uniform float fKm4PI;			// Km * 4 * PI
uniform float fScale;			// 1 / (fOuterRadius - fInnerRadius)
uniform float fScaleDepth;		// The scale depth (i.e. the altitude at which the atmosphere's average density is found)
uniform float fScaleOverScaleDepth;	// fScale / fScaleDepth
const int nSamples = 3;
const float fSamples = 3.0;
out vec3 v3Direction;
out vec3 c0;
out vec3 c1;

float scale(float fCos)
{
	float x = 1.0 - fCos;
	return fScaleDepth * exp(-0.00287 + x*(0.459 + x*(3.83 + x*(-6.80 + x*5.25))));
}

void main(void)
{
	// Get the ray from the camera to the vertex and its length (which is the far point of the ray passing through the atmosphere)
	vec3 vPosition = vec3(uWorldMatrix * vec4(position, 1.0));
	vec3 v3Ray = vPosition - v3CameraPos;
	float fFar = length(v3Ray);
	v3Ray /= fFar;

	// Calculate the closest intersection of the ray with the outer atmosphere (which is the near point of the ray passing through the atmosphere)
	float B = 2.0 * dot(v3CameraPos, v3Ray);
	float C = fCameraHeight2 - fOuterRadius2;
	float fDet = max(0.0, B*B - 4.0 * C);
	float fNear = 0.5 * (-B - sqrt(fDet));

	// Calculate the ray's starting position, then calculate its scattering offset
	vec3 v3Start = v3CameraPos + v3Ray * fNear;
	fFar -= fNear;
	float fStartAngle = dot(v3Ray, v3Start) / fOuterRadius;
	float fStartDepth = exp(-1.0 / fScaleDepth);
	float fStartOffset = fStartDepth * scale(fStartAngle);
	//c0 = vec3(1.0, 0, 0) * fStartAngle;

	// Initialize the scattering loop variables
	float fSampleLength = fFar / fSamples;
	float fScaledLength = fSampleLength * fScale;
	vec3 v3SampleRay = v3Ray * fSampleLength;
	vec3 v3SamplePoint = v3Start + v3SampleRay * 0.5;

	//gl_FrontColor = vec4(0.0, 0.0, 0.0, 0.0);
	// Now loop through the sample rays
	vec3 v3FrontColor = vec3(0.0, 0.0, 0.0);
	for(int i=0; i<nSamples; i++)
	{
		float fHeight = length(v3SamplePoint);
		float fDepth = exp(fScaleOverScaleDepth * (fInnerRadius - fHeight));
		float fLightAngle = dot(v3LightPosition, v3SamplePoint) / fHeight;
		float fCameraAngle = dot(v3Ray, v3SamplePoint) / fHeight;
		float fScatter = (fStartOffset + fDepth * (scale(fLightAngle) - scale(fCameraAngle)));
		vec3 v3Attenuate = exp(-fScatter * (v3InvWavelength * fKr4PI + fKm4PI));
		v3FrontColor += v3Attenuate * (fDepth * fScaledLength);
		v3SamplePoint += v3SampleRay;
	}

	// Finally, scale the Mie and Rayleigh colors and set up the varying variables for the pixel shader
	gl_Position = uProjectionMatrix * uViewMatrix * uWorldMatrix * vec4(position, 1.0);
	c0 = v3FrontColor * (v3InvWavelength * fKrESun);
	c1 = v3FrontColor * fKmESun;
	v3Direction = v3CameraPos - vPosition;
}

    </script>
    <script type="x-shader/x-fragment" id="atmo.frag">#version 300 es
#ifdef GL_FRAGMENT_PRECISION_HIGH
    precision highp float;
#else
    precision mediump float;
#endif

uniform vec3 v3LightPos;
uniform float g;
uniform float g2;
in vec3 v3Direction;
in vec3 c0;
in vec3 c1;

out vec4 Color;

// Calculates the Mie phase function
float getMiePhase(float fCos, float fCos2, float g, float g2)
{
	return 1.5 * ((1.0 - g2) / (2.0 + g2)) * (1.0 + fCos2) / pow(1.0 + g2 - 2.0 * g * fCos, 1.5);
}

// Calculates the Rayleigh phase function
float getRayleighPhase(float fCos2)
{
	return 0.75 + 0.75 * fCos2;
}

void main (void)
{
	float fCos = dot(v3LightPos, v3Direction) / length(v3Direction);
	float fCos2 = fCos * fCos;
	vec3 color =	getRayleighPhase(fCos2) * c0 + getMiePhase(fCos, fCos2, g, g2) * c1;
 	Color  = vec4(color, 1.0);
	Color.a = Color.b;
    //Color = vec4(1.0, 1.0, 0.0, 1.0);
}
    </script>
    <script src="./asset/js/main.js" type="module"></script>
</body>

</html>